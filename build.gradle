plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
}

group = 'com.learning'
version = '0.0.1-SNAPSHOT'
description = 'Build Dockerfile Optimization Spring Gradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springGrpcVersion', '0.12.0')
}

// -------------------- Dependencies --------------------
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation platform("org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}")
    implementation 'org.springframework.grpc:spring-grpc-server-web-spring-boot-starter'
    implementation 'io.grpc:grpc-services'

    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.grpc:spring-grpc-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
    }
}

// -------------------- Protobuf gRPC --------------------
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'annotate_code=false'
            }
        }
    }
}

// -------------------- BootJar --------------------
bootJar {
    archiveBaseName.set('springboot-gradle')
    archiveVersion.set(version)
}

// -------------------- Publishing --------------------
publishing {
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/chuvannam02/springboot-gradle")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_KEY")
            }
        }
    }
}

// Suppress metadata warnings Gradle 8+
tasks.named("generateMetadataFileForGprPublication") {
    suppressedValidationErrors.add("dependencies-without-versions")
}

// -------------------- Testing --------------------
tasks.named('test') {
    useJUnitPlatform()
}

